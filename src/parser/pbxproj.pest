file   = { SOI ~ "// !$*UTF8*$!" ~ object ~ EOI }
object = { "{" ~ field ~ (field)* ~ "}" | "{" ~ "}" }
array  = { "(" ~ value ~ ("," ~ value)* ~ ","? ~ ")" | "(" ~ ")" }
field   = { key  ~ "=" ~ value ~ ";" }

key     = @{ literal | string }
value   = {
  bool
    | number
    | string
    | array
    | object
    | literal
}

number  = @{  int ~ ("." ~ ASCII_DIGIT+)? ~ WHITESPACE }
bool    = { ("YES" | "NO") ~ WHITESPACE }
string  = @{ "\"" ~ inner ~ "\"" }
literal = @{ ("." | "/")* ~ ASCII_ALPHANUMERIC+  ~ ( ASCII_ALPHANUMERIC | "_" | "."+ | "/" )* }

inner   = @{ (!("\"" | "\\") ~ ANY)* ~ (escape ~ inner)? }
escape  = @{ "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t" | unicode) }
unicode = @{ "u" ~ (ASCII_HEX_DIGIT{4} | ASCII_HEX_DIGIT{3} ~ ASCII_ALPHA) }

int     = @{ "0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }

WHITESPACE = _{ " " | "\t" | "\r" | "\n" | ("\\" ~ unicode) }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
